repositories {
    maven {
        url 'https://raw.githubusercontent.com/felixb/mvn-repo/master/'
    }
}

apply plugin: 'com.android.application'

File releaseProps = new File("release.properties")

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "de.ub0r.android.choosebrowser"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 230010000
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            if (releaseProps.exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(releaseProps))

                storeFile file("../release.ks")
                storePassword props.getProperty("releaseKeystorePassword")
                keyAlias "jarsign"
                keyPassword props.getProperty("releaseKeyPassword")
            }
        }
    }
    buildTypes {
        debug {
            versionNameSuffix "-debug"
        }

        release {
            if (releaseProps.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'de.ub0r.android.logg0r:logg0r:2.0.0'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
